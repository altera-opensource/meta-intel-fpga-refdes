#@TYPE: Machine
#@NAME: Intel SoCFPGA Arria10
#@DESCRIPTION: Machine configuration for the Arria 10 SoCFPGA

IMAGE_TYPE:arria10 ?= "gsrd"

KERNEL_DEVICETREE:arria10 = "\
		intel/socfpga/socfpga_arria10_socdk_sdmmc.dtb \
		${@bb.utils.contains("IMAGE_TYPE", "nand", "intel/socfpga/socfpga_arria10_socdk_nand.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "qspi", "intel/socfpga/socfpga_arria10_socdk_qspi.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "intel/socfpga/fpga_static_region.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "intel/socfpga/persona0.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "intel/socfpga/persona1.dtb", "", d)} \
		"

EXTRA_IMAGEDEPENDS:append:arria10 = " \
		u-boot-socfpga-env \
		${@bb.utils.contains("IMAGE_TYPE", "pr", " u-boot-socfpga-scr", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "", " hw-ref-design", d)} \
		"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS:append:arria10 = " \
		${@bb.utils.contains("IMAGE_TYPE", "pr", " kernel-devicetree", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", " hw-ref-design", "", d)} \
		"

IMAGE_BOOT_FILES:arria10 = "\
		${@bb.utils.contains("IMAGE_TYPE", "gsrd", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pcie", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "sgmii", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		${@bb.utils.contains("IMAGE_TYPE", "tse", "socfpga_arria10_socdk_sdmmc.dtb", "", d)} \
		fit_spl_fpga_${IMAGE_TYPE}.itb;fit_spl_fpga.itb \
		${KERNEL_IMAGETYPE} \
		${UBOOT_BINARY} \
		extlinux.conf;extlinux/extlinux.conf \
		${@bb.utils.contains("IMAGE_TYPE", "pr", "boot.scr", "", d)} \
		"
